import org.gradle.util.VersionNumber

plugins {
    id "base"
    id "maven-publish"
    id "com.gradle.build-scan" version "2.3"
}

def groovyVersion = getProperty("groovyVersion")
def gradleGroovyAllVersion = getProperty("gradleGroovyAllVersion")

def release = hasProperty("release")
def snapshot = groovyVersion.endsWith("-SNAPSHOT")

group = "org.gradle.groovy"
version = "$gradleGroovyAllVersion-$groovyVersion"
description "Replacement for groovy-all.jar discontinued in Groovy 2.5"

println "The version is: $version"

buildScan {
    server = "https://e.grdev.net"
    publishAlways()
    value("RELEASED_VERSION", version)
}

allprojects {
    repositories {
        mavenCentral()
        maven { url = "https://oss.jfrog.org/artifactory/oss-snapshot-local" }
    }
}

configurations {
    groovy
    groovySources
    groovyJavadoc
    groovyGroovydoc

}

dependencies {
    groovy(group: "org.codehaus.groovy", name: "groovy-all", version: groovyVersion) {
        exclude group: 'org.codehaus.groovy', module: 'groovy-macro'
    }
    if (VersionNumber.parse(groovyVersion) >= VersionNumber.parse("2.5.0")) {
        groovy group: "org.codehaus.groovy", name: "groovy-dateutil", version: groovyVersion
    }
}

task writeVersionProperties {
    def outputFile = file("${buildDir}/tmp/gradle-groovy-all-version.properties")
    inputs.property "version", version
    outputs.file outputFile
    doLast {
        outputFile.text = "version = $version\n"
    }
}

task configureGroovyAll {
    inputs.files writeVersionProperties.outputs
    inputs.files configurations.groovy

    doFirst {
        def toMerge = [
            'LICENSE'                       : 'META-INF/LICENSE',
            'NOTICE'                        : 'META-INF/NOTICE',
            'EXTENSION_MODULE'              : 'META-INF/groovy/org.codehaus.groovy.runtime.ExtensionModule',
            'GROOVY_RELEASE_INFO_PROPERTIES': 'META-INF/groovy-release-info.properties',
            'GROOVY_RUNNER'                 : 'META-INF/services/org.apache.groovy.plugin.GroovyRunner',
            'INDEX_LIST'                    : 'META-INF/INDEX.LIST',
            'MANIFEST_MF'                   : 'META-INF/MANIFEST.MF'
        ]

        def findDuplicates = { List<FileTree> fileForest ->
            Map<String, List<File>> filesByRelativePath = [:].withDefault { [] }
            fileForest*.visit([
                visitDir : {},
                visitFile: { filesByRelativePath[it.relativePath.toString()] << it.file }
            ] as FileVisitor)
            filesByRelativePath.findAll { it.value.size() > 1 }
        }

        def concatenate = { List<File> files -> files*.text.join('\n') }

        def joinValues = { String properties, String keyName ->
            (properties =~ /$keyName *= *(.+)/)*.getAt(1).join(',')
        }

        def mergeFiles = { String relative, List<File> files ->
            switch (relative) {
                case toMerge.LICENSE:
                case toMerge.NOTICE:
                    return concatenate(files)
                case toMerge.GROOVY_RELEASE_INFO_PROPERTIES:
                    return files[0].text
                case toMerge.EXTENSION_MODULE:
                    return """
                        moduleName=groovy-all
                        moduleVersion=${joinValues(files[0].text, 'moduleVersion')}
                        extensionClasses=${joinValues(concatenate(files), 'extensionClasses')}
                        staticExtensionClasses=${joinValues(concatenate(files), 'staticExtensionClasses')}
                    """.stripIndent().trim()
                case toMerge.MANIFEST_MF:
                case toMerge.INDEX_LIST:
                case toMerge.GROOVY_RUNNER:
                    return null
                default:
                    throw new IllegalThreadStateException(files.toString())
            }
        }

        def mergeDuplicates = { FileTree tree, Map<String, List<File>> duplicates ->
            def mergedFolder = new File(tree.tree.tmpDir, 'merged')
            duplicates.each { relative, files ->
                def content = mergeFiles(relative, files)
                if (content != null) {
                    def mergedFile = new File(mergedFolder, relative)
                    mergedFile.parentFile.mkdirs()
                    mergedFile.text = content
                }
            }
            mergedFolder
        }

        tasks.groovyAll {
            from writeVersionProperties

            def fileForest = configurations.groovy
                .filter { it.name ==~ /groovy-.*\.jar/ }
                .collect { zipTree(it) }

            def duplicates = findDuplicates(fileForest)

            fileForest.each { tree ->
                from(tree) { exclude toMerge.values() }

                def mergedFolder = mergeDuplicates(tree, duplicates)
                from(mergedFolder)
            }
        }
    }
}

task groovyAll(type: Jar) {
    dependsOn configureGroovyAll
    duplicatesStrategy = DuplicatesStrategy.FAIL
    archiveBaseName = "groovy-all"
    destinationDirectory = file("build/artifacts/jar")
}

task download {
    inputs.files configurations.groovy
    outputs.dir "${buildDir}/artifacts"
    doLast {
        ArtifactResolutionResult result = dependencies.createArtifactResolutionQuery()
            .forModule("org.codehaus.groovy", "groovy-all", groovyVersion)
            .withArtifacts(JvmLibrary, SourcesArtifact, JavadocArtifact)
            .execute()
        result.resolvedComponents.each { ComponentArtifactsResult component ->
            component.getArtifacts(SourcesArtifact).each { artifact ->
                copy {
                    from artifact.file
                    into "${buildDir}/artifacts/sources"
                }
            }
            component.getArtifacts(JavadocArtifact).each { artifact ->
                copy {
                    from artifact.file
                    into "${buildDir}/artifacts/javadoc"
                }
            }
        }
    }
}

task version {
    doLast {
        println version
    }
}

def jarArtifact = artifacts.add("default", groovyAll)
def sourcesArtifact = artifacts.add("default", file("${buildDir}/artifacts/sources/groovy-all-${groovyVersion}-sources.jar")) {
    builtBy download
    classifier "sources"
}
def javadocArtifact = artifacts.add("default", file("${buildDir}/artifacts/javadoc/groovy-all-${groovyVersion}-javadoc.jar")) {
    builtBy download
    classifier "javadoc"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jarArtifact
            artifact sourcesArtifact
            artifact javadocArtifact
        }
    }
    repositories {
        maven {
            name = "local"
            url = "$buildDir/repos/external"
        }
        if (release) {
            maven {
                name = "gradle"
                def repoType = snapshot ? "snapshots" : "releases"
                url = "https://repo.gradle.org/gradle/ext-$repoType-local"
                authentication {
                    basic(BasicAuthentication) {
                        credentials {
                            username = property("artifactoryUser") ?: "nouser"
                            password = property("artifactoryPassword") ?: "nopass"
                        }
                    }
                }
            }
        }
    }
}
